plugins {
    id 'com.github.triplet.play' version '3.9.0' apply false
    id 'com.google.devtools.ksp' version '1.9.22-1.0.18' apply false
    id 'org.gradle.android.cache-fix' version '3.0.1' apply false
    id 'org.jetbrains.compose' version '1.6.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.22' apply false
    id 'org.jetbrains.kotlin.kapt' version '1.9.22' apply false
    id 'org.jetbrains.kotlin.multiplatform' version '1.9.22' apply false
    id 'org.jetbrains.kotlin.plugin.parcelize' version '1.9.22' apply false
}
apply plugin: 'com.android.application'
apply from: '../gitutils.gradle'
apply from: "$rootDir/jacoco.gradle"

def isRunningOnTravisAndIsNotPRBuild = System.getenv("CI") == "true" && file('../play.p12').exists()

if (isRunningOnTravisAndIsNotPRBuild) {
    apply plugin: 'com.github.triplet.play'
}

dependencies {
    def androidx_compose = "1.6.3"
    def androidx_paging = "3.2.1"
    def androidx_preference = "1.2.1"
    def androidx_room = "2.6.1"
    def androidx_lifecycle = "2.7.0"
    def androidx_work = "2.9.0"
    def com_google_accompanist = "0.35.0-alpha"
    def com_google_dagger = "2.50"
    def com_hannesdorfmann_adapterdelegates4 = "4.3.2"
    def com_jakewharton_butterknife = "10.2.3"
    def com_squareup_okhttp3 = "4.12.0"
    def org_jetbrains_compose = "1.6.0"

    // Jetpack Compose
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation "androidx.compose.animation:animation:$androidx_compose"
    implementation 'androidx.compose.compiler:compiler:1.5.10'
    implementation "androidx.compose.foundation:foundation:$androidx_compose"
    implementation "androidx.compose.material:material:$androidx_compose"
    implementation 'androidx.compose.material3:material3:1.2.1'
    implementation "androidx.compose.runtime:runtime:$androidx_compose"
    implementation "androidx.compose.ui:ui:$androidx_compose"
    implementation "androidx.compose.ui:ui-tooling-preview:$androidx_compose"
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.1'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$androidx_lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$androidx_lifecycle"
    implementation 'androidx.navigation:navigation-compose:2.7.7'
    implementation 'androidx.paging:paging-compose:3.2.1'
    implementation "com.google.accompanist:accompanist-adaptive:$com_google_accompanist"
    implementation "com.google.accompanist:accompanist-drawablepainter:$com_google_accompanist"
    implementation "com.google.accompanist:accompanist-navigation-material:$com_google_accompanist"
    implementation "com.google.accompanist:accompanist-permissions:$com_google_accompanist"
    implementation 'com.google.android.material:compose-theme-adapter:1.2.1'
    implementation 'com.google.android.material:compose-theme-adapter-3:1.1.1'
    implementation 'com.google.android.material:compose-theme-adapter-core:1.0.1'
    // Compose Multiplatform
    implementation "org.jetbrains.compose.animation:animation:$org_jetbrains_compose"
    implementation "org.jetbrains.compose.desktop:desktop:$org_jetbrains_compose"
    implementation "org.jetbrains.compose.foundation:foundation:$org_jetbrains_compose"
    implementation "org.jetbrains.compose.html:html-core:$org_jetbrains_compose"
    implementation "org.jetbrains.compose.material:material:$org_jetbrains_compose"
    implementation "org.jetbrains.compose.material3:material3:$org_jetbrains_compose"
    implementation "org.jetbrains.compose.runtime:runtime:$org_jetbrains_compose"
    implementation "org.jetbrains.compose.ui:ui:$org_jetbrains_compose"
    // Compose Tests
    testImplementation "androidx.compose.ui:ui-test:$androidx_compose"
    testImplementation "com.google.accompanist:accompanist-testharness:$com_google_accompanist"

    // Utils
    implementation 'in.yuvi:http.fluent:1.3'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation("com.squareup.okhttp3:okhttp:$com_squareup_okhttp3!!") {
        // Forcing dependency versions using force = true on a first-level dependency has been deprecated.
        //  Ref: https://docs.gradle.org/7.5/userguide/upgrading_version_5.html#forced_dependencies
        //force = true //API 19 support
    }
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.8.1'
    implementation 'com.squareup.okio:okio:3.8.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.jakewharton.rxbinding3:rxbinding-appcompat:3.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-support-v4:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-design:2.1.1'
    implementation 'com.facebook.fresco:fresco:3.1.3'
    implementation 'org.apache.commons:commons-lang3:3.14.0'

    // UI
    implementation 'fr.avianey.com.viewpagerindicator:library:2.4.1.1@aar'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.github.pedrovgs:renderers:4.1.0'
    implementation 'org.maplibre.gl:android-sdk:10.2.0'
    implementation 'org.maplibre.gl:android-plugin-scalebar-v9:2.0.2'

    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.github.deano2390:MaterialShowcaseView:1.3.7'
    implementation 'com.dinuscxj:circleprogressbar:1.3.6'
    implementation 'com.karumi:dexter:6.2.3'
    implementation "com.jakewharton:butterknife:$com_jakewharton_butterknife"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    kapt "com.jakewharton:butterknife-compiler:$com_jakewharton_butterknife"
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:$com_hannesdorfmann_adapterdelegates4"
    implementation "com.hannesdorfmann:adapterdelegates4-pagination:$com_hannesdorfmann_adapterdelegates4"
    implementation "androidx.paging:paging-runtime-ktx:$androidx_paging"
    testImplementation "androidx.paging:paging-common-ktx:$androidx_paging"
    implementation "androidx.paging:paging-rxjava2-ktx:$androidx_paging"
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation "com.squareup.okhttp3:okhttp-ws:$com_squareup_okhttp3"

    // Logging
    implementation 'ch.acra:acra-dialog:5.11.3'
    implementation 'ch.acra:acra-mail:5.11.3'
    implementation 'org.slf4j:slf4j-api:2.0.12'
    api('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude group: 'com.google.android', module: 'android'
    }
    implementation "com.squareup.okhttp3:logging-interceptor:$com_squareup_okhttp3"

    // Dependency injector
    implementation "com.google.dagger:dagger-android:$com_google_dagger"
    implementation "com.google.dagger:dagger-android-support:$com_google_dagger"
    kapt "com.google.dagger:dagger-android-processor:$com_google_dagger"
    kapt "com.google.dagger:dagger-compiler:$com_google_dagger"
    annotationProcessor "com.google.dagger:dagger-android-processor:$com_google_dagger"

    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.9.22'

    //Mocking
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'

    // Unit testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.test:runner:1.5.2'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'androidx.test:rules:1.5.0'
    testImplementation "com.squareup.okhttp3:mockwebserver:$com_squareup_okhttp3"
    testImplementation 'com.jraska.livedata:testing-ktx:1.3.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testImplementation 'com.facebook.soloader:soloader:0.11.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.0'
    debugImplementation("androidx.fragment:fragment-testing:1.6.2")
    testImplementation 'commons-io:commons-io:2.15.1'

    // Android testing
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.annotation:annotation:1.7.1'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.2'

    // Debugging
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.13'

    // Support libraries
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.1'

    //swipe_layout
    implementation 'com.daimajia.swipelayout:library:1.2.0@aar'

    //Room
    implementation "androidx.room:room-runtime:$androidx_room"
    implementation "androidx.room:room-ktx:$androidx_room"
    implementation "androidx.room:room-rxjava2:$androidx_room"
    kapt "androidx.room:room-compiler:$androidx_room"
    // For Kotlin use ksp instead of annotationProcessor
    testImplementation 'androidx.arch.core:core-testing:2.2.0'

    // Pref
    // Java language implementation
    implementation "androidx.preference:preference:$androidx_preference"
    // Kotlin
    implementation "androidx.preference:preference-ktx:$androidx_preference"
    //Android Media
    implementation 'com.github.juanitobananas:AndroidMediaUtil:v1.0-1'

    implementation 'androidx.multidex:multidex:2.0.1'

    // Kotlin + coroutines
    implementation "androidx.work:work-runtime-ktx:$androidx_work"
    implementation("androidx.work:work-runtime:$androidx_work")
    testImplementation "androidx.work:work-testing:$androidx_work"

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    kapt 'com.github.bumptech.glide:compiler:4.16.0'
    kaptTest 'androidx.databinding:databinding-compiler:8.3.0'
    kaptAndroidTest 'androidx.databinding:databinding-compiler:8.3.0'

    implementation("io.github.coordinates2country:coordinates2country-android:1.6") { exclude group: 'com.google.android', module: 'android' }

    //OSMDroid
    implementation("org.osmdroid:osmdroid-android:6.1.18")
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.22") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.22") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

task disableAnimations(type: Exec) {
    def adb = "$System.env.ANDROID_HOME/platform-tools/adb"
    commandLine "$adb", 'shell', 'settings', 'put', 'global', 'window_animation_scale', '0'
    commandLine "$adb", 'shell', 'settings', 'put', 'global', 'transition_animation_scale', '0'
    commandLine "$adb", 'shell', 'settings', 'put', 'global', 'animator_duration_scale', '0'
}

project.gradle.taskGraph.whenReady {
    connectedBetaDebugAndroidTest.dependsOn disableAnimations
    connectedProdDebugAndroidTest.dependsOn disableAnimations
}

android {
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.10"
    }

    kapt {
        correctErrorTypes = true
        useBuildCache = false
    }

    dataBinding {
        enabled = true
    }

    compileSdkVersion 34

    defaultConfig {
        //applicationId 'fr.free.nrw.commons'

        versionCode 1036
        versionName '4.2.1'
        setProperty("archivesBaseName", "app-commons-v$versionName-" + getBranchName())

        minSdkVersion 21
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/androidx.*']
        }
        resources {
            excludes += ['META-INF/androidx.*', 'META-INF/proguard/androidx-annotations.pro']
        }
    }


    testOptions {
        animationsDisabled true

        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }

        unitTests.all {
            jvmArgs '-noverify'
        }
    }

    sourceSets {
        // use kotlin only in tests (for now)
        test.java.srcDirs += 'src/test/kotlin'

        // use main assets and resources in test
        test.assets.srcDirs += 'src/main/assets'
        test.resources.srcDirs += 'src/main/resoures'
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            testProguardFile 'test-proguard-rules.txt'
            signingConfig signingConfigs.debug
            if (isRunningOnTravisAndIsNotPRBuild) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            testCoverageEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            testProguardFile 'test-proguard-rules.txt'
            versionNameSuffix "-debug-" + getBranchName()
        }
    }

    if (isRunningOnTravisAndIsNotPRBuild) {
        // configure keystore based on env vars in Travis for automated alpha builds
        signingConfigs.release.storeFile = file("../nr-commons.keystore")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("key_alias")
        signingConfigs.release.keyPassword = System.getenv("key_password")
    }

    configurations.all {
        resolutionStrategy.force 'androidx.annotation:annotation:1.7.1'
        resolutionStrategy.force 'com.jakewharton.timber:timber:5.0.1'
        resolutionStrategy.force 'androidx.fragment:fragment:1.6.2'
        exclude module: 'okhttp-ws'
    }
    flavorDimensions 'tier'
    productFlavors {
        prod {

            applicationId 'fr.free.nrw.commons'

            buildConfigField "String", "WIKIMEDIA_API_POTD", "\"https://commons.wikimedia.org/w/api.php?action=featuredfeed&feed=potd&feedformat=rss&language=en\""
            buildConfigField "String", "WIKIMEDIA_API_HOST", "\"https://commons.wikimedia.org/w/api.php\""
            buildConfigField "String", "WIKIDATA_API_HOST", "\"https://www.wikidata.org/w/api.php\""
            buildConfigField "String", "WIKIDATA_URL", "\"https://www.wikidata.org\""
            buildConfigField "String", "WIKIMEDIA_FORGE_API_HOST", "\"https://tools.wmflabs.org/\""
            buildConfigField "String", "WIKIMEDIA_CAMPAIGNS_URL", "\"https://raw.githubusercontent.com/commons-app/campaigns/master/campaigns.json\""
            buildConfigField "String", "IMAGE_URL_BASE", "\"https://upload.wikimedia.org/wikipedia/commons\""
            buildConfigField "String", "HOME_URL", "\"https://commons.wikimedia.org/wiki/\""
            buildConfigField "String", "COMMONS_URL", "\"https://commons.wikimedia.org\""
            buildConfigField "String", "WIKIDATA_URL", "\"https://www.wikidata.org\""
            buildConfigField "String", "MOBILE_HOME_URL", "\"https://commons.m.wikimedia.org/wiki/\""
            buildConfigField "String", "SIGNUP_LANDING_URL", "\"https://commons.m.wikimedia.org/w/index.php?title=Special:CreateAccount&returnto=Main+Page&returntoquery=welcome%3Dyes\""
            buildConfigField "String", "SIGNUP_SUCCESS_REDIRECTION_URL", "\"https://commons.m.wikimedia.org/w/index.php?title=Main_Page&welcome=yes\""
            buildConfigField "String", "FORGOT_PASSWORD_URL", "\"https://commons.wikimedia.org/wiki/Special:PasswordReset\""
            buildConfigField "String", "PRIVACY_POLICY_URL", "\"https://github.com/commons-app/commons-app-documentation/blob/master/android/Privacy-policy.md\""
            buildConfigField "String", "ACCOUNT_TYPE", "\"fr.free.nrw.commons\""
            buildConfigField "String", "CONTRIBUTION_AUTHORITY", "\"fr.free.nrw.commons.contributions.contentprovider\""
            buildConfigField "String", "MODIFICATION_AUTHORITY", "\"fr.free.nrw.commons.modifications.contentprovider\""
            buildConfigField "String", "CATEGORY_AUTHORITY", "\"fr.free.nrw.commons.categories.contentprovider\""
            buildConfigField "String", "RECENT_SEARCH_AUTHORITY", "\"fr.free.nrw.commons.explore.recentsearches.contentprovider\""
            buildConfigField "String", "RECENT_LANGUAGE_AUTHORITY", "\"fr.free.nrw.commons.recentlanguages.contentprovider\""
            buildConfigField "String", "BOOKMARK_AUTHORITY", "\"fr.free.nrw.commons.bookmarks.contentprovider\""
            buildConfigField "String", "BOOKMARK_LOCATIONS_AUTHORITY", "\"fr.free.nrw.commons.bookmarks.locations.contentprovider\""
            buildConfigField "String", "BOOKMARK_ITEMS_AUTHORITY", "\"fr.free.nrw.commons.bookmarks.items.contentprovider\""
            buildConfigField "String", "COMMIT_SHA", "\"" + getBuildVersion().toString() + "\""
            buildConfigField "String", "TEST_USERNAME", "\"" + getTestUserName() + "\""
            buildConfigField "String", "TEST_PASSWORD", "\"" + getTestPassword() + "\""
            buildConfigField "String", "DEPICTS_PROPERTY", "\"P180\""
            dimension 'tier'
        }

        beta {
            applicationId 'fr.free.nrw.commons.beta'

            // What values do we need to hit the BETA versions of the site / api ?
            buildConfigField "String", "WIKIMEDIA_API_POTD", "\"https://commons.wikimedia.org/w/api.php?action=featuredfeed&feed=potd&feedformat=rss&language=en\""
            buildConfigField "String", "WIKIMEDIA_API_HOST", "\"https://commons.wikimedia.beta.wmflabs.org/w/api.php\""
            buildConfigField "String", "WIKIDATA_API_HOST", "\"https://www.wikidata.org/w/api.php\""
            buildConfigField "String", "WIKIDATA_URL", "\"https://www.wikidata.org\""
            buildConfigField "String", "WIKIMEDIA_FORGE_API_HOST", "\"https://tools.wmflabs.org/\""
            buildConfigField "String", "WIKIMEDIA_CAMPAIGNS_URL", "\"https://raw.githubusercontent.com/commons-app/campaigns/master/campaigns_beta_active.json\""
            buildConfigField "String", "IMAGE_URL_BASE", "\"https://upload.beta.wmflabs.org/wikipedia/commons\""
            buildConfigField "String", "HOME_URL", "\"https://commons.wikimedia.beta.wmflabs.org/wiki/\""
            buildConfigField "String", "COMMONS_URL", "\"https://commons.wikimedia.beta.wmflabs.org\""
            buildConfigField "String", "WIKIDATA_URL", "\"https://www.wikidata.org\""
            buildConfigField "String", "MOBILE_HOME_URL", "\"https://commons.m.wikimedia.beta.wmflabs.org/wiki/\""
            buildConfigField "String", "SIGNUP_LANDING_URL", "\"https://commons.m.wikimedia.beta.wmflabs.org/w/index.php?title=Special:CreateAccount&returnto=Main+Page&returntoquery=welcome%3Dyes\""
            buildConfigField "String", "SIGNUP_SUCCESS_REDIRECTION_URL", "\"https://commons.m.wikimedia.beta.wmflabs.org/w/index.php?title=Main_Page&welcome=yes\""
            buildConfigField "String", "FORGOT_PASSWORD_URL", "\"https://commons.wikimedia.beta.wmflabs.org/wiki/Special:PasswordReset\""
            buildConfigField "String", "PRIVACY_POLICY_URL", "\"https://github.com/commons-app/commons-app-documentation/blob/master/android/Privacy-policy.md\""
            buildConfigField "String", "ACCOUNT_TYPE", "\"fr.free.nrw.commons.beta\""
            buildConfigField "String", "CONTRIBUTION_AUTHORITY", "\"fr.free.nrw.commons.beta.contributions.contentprovider\""
            buildConfigField "String", "MODIFICATION_AUTHORITY", "\"fr.free.nrw.commons.beta.modifications.contentprovider\""
            buildConfigField "String", "CATEGORY_AUTHORITY", "\"fr.free.nrw.commons.beta.categories.contentprovider\""
            buildConfigField "String", "RECENT_SEARCH_AUTHORITY", "\"fr.free.nrw.commons.beta.explore.recentsearches.contentprovider\""
            buildConfigField "String", "RECENT_LANGUAGE_AUTHORITY", "\"fr.free.nrw.commons.beta.recentlanguages.contentprovider\""
            buildConfigField "String", "BOOKMARK_AUTHORITY", "\"fr.free.nrw.commons.beta.bookmarks.contentprovider\""
            buildConfigField "String", "BOOKMARK_LOCATIONS_AUTHORITY", "\"fr.free.nrw.commons.beta.bookmarks.locations.contentprovider\""
            buildConfigField "String", "BOOKMARK_ITEMS_AUTHORITY", "\"fr.free.nrw.commons.beta.bookmarks.items.contentprovider\""
            buildConfigField "String", "COMMIT_SHA", "\"" + getBuildVersion().toString() + "\""
            buildConfigField "String", "TEST_USERNAME", "\"" + getTestUserName() + "\""
            buildConfigField "String", "TEST_PASSWORD", "\"" + getTestPassword() + "\""
            buildConfigField "String", "DEPICTS_PROPERTY", "\"P245962\""
            dimension 'tier'
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    buildToolsVersion '34.0.0'

    buildFeatures {
        buildConfig = true
        compose = true
        viewBinding true
    }
    namespace 'fr.free.nrw.commons'
    lint {
        abortOnError false
        disable 'MissingTranslation', 'ExtraTranslation'
    }
    ndkVersion '26.2.11394342'
}

String getTestUserName() {
    def propFile = rootProject.file("./local.properties")
    def properties = new Properties()
    properties.load(new FileInputStream(propFile))
    return properties['TEST_USER_NAME']
}

String getTestPassword() {
    def propFile = rootProject.file("./local.properties")
    def properties = new Properties()
    properties.load(new FileInputStream(propFile))
    return properties['TEST_USER_PASSWORD']
}

if (isRunningOnTravisAndIsNotPRBuild) {
    play {
        track = "alpha"
        userFraction = 1
        serviceAccountEmail = System.getenv("SERVICE_ACCOUNT_NAME")
        serviceAccountCredentials = file("../play.p12")

        resolutionStrategy = "auto"
        outputProcessor { // this: ApkVariantOutput
            versionNameOverride = "$versionNameOverride.$versionCode"
        }
    }
}
